<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4026.8">
  <POU Name="MAIN" Id="{e5121501-3133-07a7-12bf-96dcd5d9e348}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM MAIN
VAR	
	XTS				: FB_XTS<6>;
	MainCommands	: MachineCmd_typ;
	// Machine
	State			: MachineState_enum;

	Init			: BOOL;
	// ========= Example Application Variables =========

    nextStation 			: USINT := 4;
                    	
    lowVelocity    			: LREAL := 200;  	// mm/s
    mediumVelocity 			: LREAL := 800;  	// mm/s
    highVelocity   			: LREAL := 1200; 	// mm/s
	
	StationTimer 			: ARRAY[0..Param.NUM_STATIONS] OF TON; // timer blocks, for station dwells
	InitialParameterSet		: MotionParameters_typ;
	RecoveryComplete		: BOOL;
	
	mcDirectionPositive		: Tc2_MC2.MC_Direction;
	Trig0MoverIndex			: UDINT;
	Trig0MoverPosLag		: LREAL;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[
(* ======================================================================================

--- XTS Starter Project ---

This MAIN (PRG) is intended to serve as an example, which can be modified to suit your specific application.

No programming changes should be necessary inside the provided XTS objects / function block definitions.
They are intended to be simply instantiated and controlled via the provided methods and properties.
However the objects are provided as open-source for modification and extension as needed.

Documentation for these objects can be found online at:
https://beckhoff-usa-community.github.io/XTS_Base/

Bug reports, new feature requests, and general feedback is always welcome on our Github page.
For any other questions, please reach out to your local Application Engineer.

- XTS Application Group | Beckhoff USA

====================================================================================== *)

(*
All sample code provided by Beckhoff Automation LLC are for illustrative purposes only
and are provided “as is” and without any warranties, express or implied. Actual
implementations in applications will vary significantly. Beckhoff Automation LLC shall
have no liability for, and does not waive any rights in relation to, any code samples that it
provides or the use of such code samples for any purpose.
*)



IF NOT Init THEN Initializing(); END_IF

CASE State OF 

	MS_OFF:
		IF MainCommands.Enable THEN
			XTS.Enable				:= TRUE;
			State					:= MS_RESTARTING;
		END_IF

	MS_RESTARTING:
		IF NOT MainCommands.Enable THEN
			XTS.Enable				:= FALSE;
			State					:= MS_STOPPING;
		ELSIF XTS.State = XTS_ENABLED THEN
			State		:= MS_ENABLED;
		END_IF
	
	MS_ENABLED:
		IF NOT MainCommands.Enable THEN
			XTS.Enable				:= FALSE;
			State					:= MS_STOPPING;
		ELSIF MainCommands.Start THEN
			State					:= MS_ONESHOT_RECOVER;
		END_IF
		
	MS_ONESHOT_RECOVER:
		// one time commands before starting typical process motions
		RecoverOneshot();
		State := MS_RECOVERING;
		
	MS_RECOVERING:
		// wait for any recovery actions to complete
		Recovering();
		// test for errors, commands or recovery complete
		IF MainCommands.Stop THEN
			MainCommands.Start		:= FALSE;
			XTS.System.CompleteMoverList.HaltAll();
            State         	:= MS_STOPPING;
        ELSIF NOT MainCommands.Enable THEN
            XTS.Enable		:= FALSE;
			XTS.System.CompleteMoverList.HaltAll();
            State          			:= MS_STOPPING;
		ELSIF RecoveryComplete THEN
			// recovery sequence is complete, move on to run state
			State					:= MS_RUNNING;
        END_IF

	MS_RUNNING:
		// run the station-to-station mover logic
		StationLogic();
		
		// monitor for disable
		IF NOT MainCommands.Enable THEN
			XTS.Enable				:= FALSE;
			XTS.System.CompleteMoverList.HaltAll();
			State					:= MS_STOPPING;
		ELSIF MainCommands.Stop THEN
			MainCommands.Start		:= FALSE;
			XTS.System.CompleteMoverList.HaltAll();
			State					:= MS_STOPPING;
		END_IF

	MS_STOPPING:
		IF NOT MainCommands.Enable THEN
			XTS.Enable 				:= FALSE;
		END_IF
		IF XTS.State = XTS_DISABLED THEN
			State					:= MS_OFF;
		ELSIF  MainCommands.Start THEN
			MainCommands.Stop		:= FALSE;
			State					:= MS_ONESHOT_RECOVER;
		END_IF
	
END_CASE

XTS.CyclicLogic();
]]></ST>
    </Implementation>
    <Action Name="Initializing" Id="{0bad743d-cb82-0efe-1988-42b77153463d}">
      <Implementation>
        <ST><![CDATA[// XTS Object Init
XTS.Station[0].Position 		:= 5;
XTS.Station[1].Position 		:= 750;
XTS.Station[2].Position 		:= 1200;
XTS.Station[3].Position 		:= 1300;
XTS.Station[4].Position 		:= 1400;

XTS.PositionTrigger[0].Position := 1500;
XTS.PositionTrigger[1].Position := 2000;
XTS.PositionTrigger[2].Position := 3500;

XTS.Zone[0].StartPosition		:= 3750;
XTS.Zone[0].EndPosition   		:= 750;

XTS.Zone[1].StartPosition 		:= 750;
XTS.Zone[1].EndPosition   		:= 1750;

XTS.Zone[2].StartPosition 		:= 1750;
XTS.Zone[2].EndPosition   		:= 2750;

XTS.Zone[3].StartPosition		:= 2750;
XTS.Zone[3].EndPosition   		:= 3750;

XTS.InitialParameterSet.Velocity		:= 1000;		// mm/s
XTS.InitialParameterSet.Acceleration	:= 10_000;		// mm/s2
XTS.InitialParameterSet.Deceleration	:= 10_000;		// mm/s2
XTS.InitialParameterSet.Jerk			:= 100_000;		// mm/s3
XTS.InitialParameterSet.Gap				:= 85;			// mm
XTS.InitialParameterSet.Direction		:= mcDirectionPositive;

LowVelocity		:= 300;
MediumVelocity	:= 800;
HighVelocity	:= 1200;

Init	:= TRUE;]]></ST>
      </Implementation>
    </Action>
    <Action Name="Recovering" Id="{4b8b03ac-750a-0276-1b31-6724bd5608d0}">
      <Implementation>
        <ST><![CDATA[// this state is called until all movers have completed their desired recovery tasks

// wait for all movers to reach the standstill state and one mover at station 0
IF (XTS.System.CompleteMoverList.IsAllMoversHalted AND XTS.Station[0].MoverInPosition) THEN
	RecoveryComplete := TRUE;
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="RecoverOneshot" Id="{b08aba9f-35eb-0e63-04bc-615de48f82c9}">
      <Implementation>
        <ST><![CDATA[// this action is called one time after the start command is issued
// it is intended to provide a single command to each mover to facilitate recovery
// other commands such as resetting counters and timers should also be carried out here

// send all movers to station 0
XTS.System.CompleteMoverList.SetAllVelocity(mediumVelocity).MoveAllToStation( XTS.Station[0] );
XTS.System.ResetStatistics();

// Reinit nextStation
nextStation := 4;]]></ST>
      </Implementation>
    </Action>
    <Action Name="StationLogic" Id="{1adacb9d-add1-0f3c-1a94-78c322ce5f05}">
      <Implementation>
        <ST><![CDATA[		(*Mover routing logic should be written here to define the system's operating behavior.
		Station implementations, Position Trigger logic, etc. In general, you can remove the example code
		here down to the short line break that looks like this: // ============= *)
	
        // Station 0 Logic
        IF XTS.Station[0].MoverInPosition THEN
            StationTimer[0](IN := TRUE, PT := T#1000MS);
            IF StationTimer[0].Q THEN
                XTS.Mover[XTS.Station[0].CurrentMover.MoverIndex].MoveToStationByRampTime(
					DestinationStation:= XTS.Station[1], 
					Velocity:= 500, 
					RampTime:= 1.5, 
					Stiffness:= 0.5, 
					Gap:= 85);
            END_IF
        ELSE
            StationTimer[0](IN := FALSE);
        END_IF
		
        // Station 1 Logic		
        IF XTS.Station[1].MoverInPosition THEN
            StationTimer[1](IN := TRUE, PT := T#250MS);
            IF StationTimer[1].Q THEN
                XTS.Station[1].CurrentMover.MoveToStation(XTS.Station[nextStation]);
                nextStation := nextStation - 1;
                IF nextStation = 1 THEN
                    nextStation := 4;
                END_IF
            END_IF
        ELSE
            StationTimer[1](IN := FALSE);
        END_IF
		
		// Group of Stations 2, 3, and 4 Logic
        IF XTS.Station[2].MoverInPosition AND XTS.Station[3].MoverInPosition AND XTS.Station[4].MoverInPosition THEN
            StationTimer[2](IN := TRUE, PT := T#750MS);
            IF StationTimer[2].Q THEN
				StationTimer[2](IN := FALSE);
				
				XTS.Station[2].CurrentMover.MotionParameters:= XTS.InitialParameterSet;
				XTS.Station[3].CurrentMover.MotionParameters:= XTS.InitialParameterSet;
				XTS.Station[4].CurrentMover.MotionParameters:= XTS.InitialParameterSet;

                XTS.Station[2].CurrentMover.MoveVelocity(500);
                XTS.Station[3].CurrentMover.MoveVelocity(500);
                XTS.Station[4].CurrentMover.MoveVelocity(500);
				
            END_IF
        END_IF
		
        // Position Trigger 0 Logic
        IF XTS.PositionTrigger[0].MoverPassedPosition THEN
            XTS.PositionTrigger[0].CurrentMover.SetVelocity(lowVelocity);
			
			// To Call ApplicationMover addon Method/Properties/Variables, you must call Mover[] array index directly
			// Get MoverIndex for later use with ApplicationMover addons
			Trig0MoverIndex		:= XTS.PositionTrigger[0].CurrentMover.MoverIndex;
			
			// Call ApplicationMover Method/Properties/Variables using previously latched MoverIndex
			Trig0MoverPosLag	:= XTS.Mover[Trig0MoverIndex].PositionLag;
			
            XTS.PositionTrigger[0].MuteCurrent();
						
        END_IF
		
        // Position Trigger 1 Logic
        IF XTS.PositionTrigger[1].MoverPassedPosition THEN
			XTS.PositionTrigger[1].CurrentMover.SetAcceleration(5E2);
            XTS.PositionTrigger[1].CurrentMover.SetVelocity(highVelocity);
            XTS.PositionTrigger[1].MuteCurrent();
        END_IF

		// Position Trigger 2 Logic
        IF XTS.PositionTrigger[2].MoverPassedPosition THEN
			XTS.PositionTrigger[2].CurrentMover.SetAcceleration(1E4);
            XTS.PositionTrigger[2].CurrentMover.SetVelocity(lowVelocity);
            XTS.PositionTrigger[2].CurrentMover.MoveToStation(XTS.Station[0]);
            XTS.PositionTrigger[2].MuteCurrent();
        END_IF]]></ST>
      </Implementation>
    </Action>
    <LineIds Name="MAIN">
      <LineId Id="96" Count="0" />
      <LineId Id="102" Count="17" />
      <LineId Id="97" Count="0" />
      <LineId Id="95" Count="0" />
      <LineId Id="80" Count="0" />
      <LineId Id="98" Count="3" />
      <LineId Id="81" Count="1" />
      <LineId Id="88" Count="2" />
      <LineId Id="3" Count="76" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="MAIN.Initializing">
      <LineId Id="2" Count="33" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="MAIN.Recovering">
      <LineId Id="2" Count="4" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="MAIN.RecoverOneshot">
      <LineId Id="2" Count="8" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="MAIN.StationLogic">
      <LineId Id="2" Count="77" />
      <LineId Id="1" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>