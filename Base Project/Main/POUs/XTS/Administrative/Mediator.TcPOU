<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="Mediator" Id="{164d51b4-07ec-0e46-0eb0-392af39d7d3a}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK Mediator
VAR_INPUT

	MoverArray				: ARRAY [0..GVL.NUM_MOVERS-1] OF POINTER TO Mover;
	
	MoverListArray			: ARRAY [0..GVL.NUM_MOVERLISTS-1] OF POINTER TO MoverList;
	PositionTriggerArray	: ARRAY [0..GVL.NUM_POSITIONTRIGGERS-1] OF POINTER TO PositionTrigger;
	StationArray			: ARRAY [0..GVL.NUM_STATIONS-1] OF POINTER TO Station;
	ZoneArray				: ARRAY [0..GVL.NUM_ZONES-1] OF POINTER TO Zone;
	TrackArray				: ARRAY [0..GVL.NUM_TRACKS] OF POINTER TO Track;	// track 0 is a special absolute case, this makes the array 1 larger than normal	

	GROUP_REF				: AXES_GROUP_REF;
	
END_VAR
VAR_OUTPUT
	Error			: BOOL;			// error is currently active
	ErrorID			: UDINT;		// errorID of the active error
	ErrorOrigin		: STRING;		// error source, in case of errored internal function block
END_VAR
VAR
	{attribute 'instance-path'}
	{attribute 'noinit'}
	{attribute 'hide'}
	InstancePath		: STRING;	// instance naming, for diagnostics purposes

	internalMoverList				: MoverList;;
	internalM1DetectFlag			: BOOL;
	internalM1DetectState			: INT;
	internalEnableGroupFlag			: BOOL;
	internalEnableGroupState		: INT;
	internalEnabled					: BOOL;
	
	// ========= XTS Utility Diagnostics =========
	fbXtsEnvironment				: FB_TcIoXtsEnvironment;
	fbXtsEnvironmentVisu			: FB_XtsEnvironmentVisu;
	stXtsEnvironmentConfiguration	: ST_XtsEnvironmentConfiguration;	
	nEnvironmentState				: INT;

	internalEnvironment				: I_TcIoXtsEnvironment;
	internalXPU						: I_TcIoXtsProcessingUnit;
	internalInfoServer				: I_TcIoXtsInfoServer;
	
	// ========= internal function blocks =========
	fbGroupStatus			: MC_GroupReadStatus;
	fbGroupEnable			: MC_GroupEnable;
	fbGroupReset			: MC_GroupReset;
	fbGroupStop				: MC_GroupStop;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Methods" Id="{0d177045-c92a-0627-14e7-41e4c9da72c3}" />
    <Folder Name="Properties" Id="{d9d81742-b9bd-0437-3780-4a446062d311}" />
    <Method Name="AddMover" Id="{3437c27c-dd7b-0ac3-07a2-d432fc68235a}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD AddMover : BOOL
VAR_INPUT
	
	Mover		: REFERENCE TO Mover;

END_VAR

VAR
	duplicateFound		: BOOL;
	i					: UDINT;
	emptyIDX			: UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[

// Check for duplicates

IF __ISVALIDREF( Mover ) THEN
	
	Mover.Mediator			REF= THIS^;

	duplicateFound			:= FALSE;
	
	FOR i := 0 TO SIZEOF( MoverArray ) / SIZEOF( MoverArray[0] ) - 1 DO
		IF MoverArray[i] <> 0 THEN		// verify the element is clear to prevent overwrites
			IF ADR(Mover) = MoverArray[i] THEN
				duplicateFound		:= TRUE;	// this mover is already mediated
			END_IF
		ELSE
			emptyIDX	:= i;	// confirmed oopen array element
		END_IF	
	END_FOR
	
	IF duplicateFound = FALSE THEN
		
		THIS^.MoverArray[emptyIDX]		:= ADR( Mover );	// new mover into empty array element

		// Automatically register the mover with any Position Triggers
		FOR i := 0 TO SIZEOF( PositionTriggerArray ) / SIZEOF( PositionTriggerArray[0] ) - 1 DO
			IF PositionTriggerArray[i] <> 0 THEN
				PositionTriggerArray[i]^.RegisterMover( Mover );
			END_IF			
		END_FOR
		
		// Automatically register the mover with any Zones
		FOR i := 0 TO SIZEOF( ZoneArray ) / SIZEOF( ZoneArray[0] ) - 1 DO
			IF ZoneArray[i] <> 0 THEN
				ZoneArray[i]^.RegisterMover( Mover );
			END_IF
		END_FOR
		
	END_IF
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddMoverList" Id="{fa372345-a99c-01d2-2d94-cc2fd6bab53d}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD AddMoverList : BOOL
VAR_INPUT
	
	MoverList		: REFERENCE TO MoverList;

END_VAR

VAR
	duplicateFound		: BOOL;
	i					: UDINT;
	emptyIDX			: UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[

// Check for duplicates
IF __ISVALIDREF( MoverList ) THEN
	
	MoverList.Mediator			REF= THIS^;
	
	duplicateFound				:= FALSE;
	
	FOR i := 0 TO SIZEOF( MoverListArray ) / SIZEOF( MoverListArray[0] ) - 1 DO
		IF MoverListArray[i] <> 0 THEN		// verify the element is clear to prevent overwrites
			IF ADR( MoverList ) = MoverListArray[i] THEN
				duplicateFound		:= TRUE;		// this moverlist is already mediated
			END_IF
		ELSE
			emptyIDX		:= i;		// confirmed open array element
		END_IF
	END_FOR
	
	IF duplicateFound = FALSE THEN
		THIS^.MoverListArray[emptyIDX]		:= ADR( MoverList );
	END_IF
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddPositionTrigger" Id="{37697d0e-c381-0846-3d01-3d7f2d276ad1}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD AddPositionTrigger : BOOL
VAR_INPUT
	
	PositionTrigger		: REFERENCE TO PositionTrigger;

END_VAR

VAR
	duplicateFound		: BOOL;
	i					: UDINT;
	emptyIDX			: UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[

// Check for duplicates
IF __ISVALIDREF( PositionTrigger ) THEN
	
	PositionTrigger.Mediator		REF= THIS^;
	
	duplicateFound					:= FALSE;
	
	FOR i := 0 TO SIZEOF( PositionTriggerArray ) / SIZEOF( PositionTriggerArray[0] ) - 1 DO
		IF PositionTriggerArray[i] <> 0 THEN		// verify the element is clear to prevent overwrites
			IF ADR( PositionTrigger ) = PositionTriggerArray[i] THEN
				duplicateFound		:= TRUE;		// this moverlist is already mediated
			END_IF
		ELSE
			emptyIDX		:= i;		// confirmed open array element
		END_IF
	END_FOR
	
	IF duplicateFound = FALSE THEN
		THIS^.PositionTriggerArray[emptyIDX]		:= ADR( PositionTrigger );
		
		// Automatically register all existing movers with the new position trigger
		FOR i := 0 TO SIZEOF( MoverArray ) / SIZEOF( MoverArray[0] ) - 1 DO
			IF MoverArray[i] <> 0 THEN
				THIS^.PositionTriggerArray[emptyIDX]^.RegisterMover( MoverArray[i]^ );
			END_IF
		END_FOR		
		
	END_IF
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddStation" Id="{158bfd8c-35c0-05f6-1398-0d0a087d099a}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD AddStation : BOOL
VAR_INPUT
	
	Station		: REFERENCE TO Station;

END_VAR

VAR
	duplicateFound		: BOOL;
	i					: UDINT;
	emptyIDX			: UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[

// Check for duplicates
IF __ISVALIDREF( Station ) THEN
	
	Station.Mediator	REF= THIS^;
	
	duplicateFound		:= FALSE;
	
	FOR i := 0 TO SIZEOF( StationArray ) / SIZEOF( StationArray[0] ) - 1 DO
		IF StationArray[i] <> 0 THEN		// verify the element is clear to prevent overwrites
			IF ADR( Station ) = StationArray[i] THEN
				duplicateFound		:= TRUE;		// this moverlist is already mediated
			END_IF
		ELSE
			emptyIDX		:= i;		// confirmed open array element
		END_IF
	END_FOR
	
	IF duplicateFound = FALSE THEN
		THIS^.StationArray[emptyIDX]		:= ADR( Station );
	END_IF
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddTrack" Id="{1f5fda23-0b4f-059d-05b7-42b4aba73321}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD AddTrack : BOOL
VAR_INPUT
	
	Track		: REFERENCE TO Track;

END_VAR

VAR
	duplicateFound		: BOOL;
	i					: UDINT;
	emptyIDX			: UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[

// Check for duplicates
IF __ISVALIDREF( Track ) THEN
	
	Track.Mediator		REF= THIS^;
	
	duplicateFound		:= FALSE;
	
	FOR i := 0 TO SIZEOF( TrackArray ) / SIZEOF( TrackArray[0] ) - 1 DO
		IF TrackArray[i] <> 0 THEN		// verify the element is clear to prevent overwrites
			IF ADR( Track ) = TrackArray[i] THEN
				duplicateFound		:= TRUE;		// this moverlist is already mediated
			END_IF
		ELSE
			emptyIDX		:= i;		// confirmed open array element
		END_IF
	END_FOR
	
	IF duplicateFound = FALSE THEN
		THIS^.TrackArray[emptyIDX]		:= ADR( Track );
	END_IF
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddZone" Id="{b7275e17-ff08-069c-32b8-5e7a13dacb64}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD AddZone : BOOL
VAR_INPUT
	
	Zone		: REFERENCE TO Zone;

END_VAR

VAR
	duplicateFound		: BOOL;
	i					: UDINT;
	emptyIDX			: UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[

// Check for duplicates
IF __ISVALIDREF( Zone ) THEN
	
	Zone.Mediator		REF= THIS^;
	
	duplicateFound		:= FALSE;
	
	FOR i := 0 TO SIZEOF( ZoneArray ) / SIZEOF( ZoneArray[0] ) - 1 DO
		IF ZoneArray[i] <> 0 THEN		// verify the element is clear to prevent overwrites
			IF ADR( Zone ) = ZoneArray[i] THEN
				duplicateFound		:= TRUE;		// this moverlist is already mediated
			END_IF
		ELSE
			emptyIDX		:= i;		// confirmed open array element
		END_IF
	END_FOR
	
	IF duplicateFound = FALSE THEN
		THIS^.ZoneArray[emptyIDX]		:= ADR( Zone );
		
		// Automatically register all existing movers with the new zone
		FOR i := 0 TO SIZEOF( MoverArray ) / SIZEOF( MoverArray[0] ) - 1 DO
			IF MoverArray[i] <> 0 THEN
				THIS^.ZoneArray[emptyIDX]^.RegisterMover( MoverArray[i]^ );
			END_IF
		END_FOR
		
	END_IF
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="CompleteMoverList" Id="{41f8f92e-6d81-02a1-048b-3da9baf27ff9}" FolderPath="Properties\">
      <Declaration><![CDATA[PROPERTY CompleteMoverList : iMoverList]]></Declaration>
      <Get Name="Get" Id="{0ba48769-41e2-05d1-1ed6-465e4a354ad7}">
        <Declaration><![CDATA[VAR
	i: UDINT;
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[internalMoverList.UnregisterAll();

FOR i := 0 TO SIZEOF( MoverArray ) / SIZEOF( MoverArray[0] ) - 1 DO
	IF MoverArray[i] <> 0 THEN
		internalMoverList.RegisterMover( MoverArray[i]^ );
	END_IF
END_FOR

CompleteMoverList	:= internalMoverList;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Cyclic" Id="{f2264867-ad86-0c96-3c42-ade0fe3bf9e2}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD Cyclic : BOOL
VAR_INPUT
END_VAR

VAR
	i: ULINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Handle all project cyclic calls

FOR i := 0 TO GVL.NUM_MOVERS - 1 DO
	IF MoverArray[i] <> 0 THEN
		MoverArray[i]^.Cyclic( GROUP_REF );			// could make this ca group addition clener
		MoverArray[i]^.CyclicTrack();
		MoverArray[i]^.AxisReference.ReadStatus();
	END_IF
END_FOR

FOR i := 0 TO GVL.NUM_STATIONS - 1 DO
	IF StationArray[i] <> 0 THEN
		StationArray[i]^.Cyclic();
	END_IF
END_FOR

FOR i := 0 TO GVL.NUM_POSITIONTRIGGERS - 1 DO
	IF PositionTriggerArray[i] <> 0 THEN
		PositionTriggerArray[i]^.Cyclic();
	END_IF
END_FOR

FOR i := 0 TO GVL.NUM_TRACKS - 1 DO
	IF TrackArray[i] <> 0 THEN
		TrackArray[i]^.Cyclic();
	END_IF
END_FOR

CyclicEnvironment();
CyclicMover1();
CyclicEnableGroup();]]></ST>
      </Implementation>
    </Method>
    <Method Name="CyclicEnableGroup" Id="{29279241-cafd-03e6-374f-d434ad56f253}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD CyclicEnableGroup : BOOL
VAR_INPUT
END_VAR
VAR_INST
	MsgCreate		: FB_TcMessage;	
END_VAR
VAR

END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// get group status for property
fbGroupStatus.Enable := TRUE;
fbGroupStatus( AxesGroup := GROUP_REF );

CASE internalEnableGroupState OF
	0:	// wait for flag 
		IF internalEnableGroupFlag AND NOT internalEnabled THEN
			internalEnableGroupState := 10;
			Error := FALSE;
		END_IF;

	10: // ------------------------------------------------------- check the status of the group
	
		IF fbGroupStatus.Valid AND fbGroupStatus.GroupErrorStop THEN
			fbGroupReset.Execute		:= TRUE;
		ELSIF fbGroupStatus.Valid AND fbGroupStatus.GroupDisabled THEN
			fbGroupReset.Execute		:= FALSE;
			internalEnableGroupState	:= 20;
		ELSIF fbGroupStatus.Valid AND ( fbGroupStatus.GroupStandby OR fbGroupStatus.GroupMoving ) THEN
			fbGroupReset.Execute		:= FALSE;
			internalEnableGroupState	:= 100;
		END_IF
		
		IF fbGroupStatus.Error THEN
			ErrorID						:= fbGroupStatus.ErrorId;
			ErrorOrigin					:= CONCAT( InstancePath, '.fbGroupStatus' );
			MsgCreate.CreateEx( TC_EVENTS.XtsBaseEventClass.fbGroupStatus,0 );
			MsgCreate.ipArguments.Clear().AddString(THIS^.InstancePath).AddUDINT(THIS^.ErrorID);
			internalEnableGroupState	:= 900;
		ELSIF fbGroupReset.Error THEN
			ErrorID						:= fbGroupReset.ErrorId;
			ErrorOrigin					:= CONCAT( InstancePath, '.fbGroupReset' );
			MsgCreate.CreateEx( TC_EVENTS.XtsBaseEventClass.fbGroupReset,0 );
			MsgCreate.ipArguments.Clear().AddString(THIS^.InstancePath).AddUDINT(THIS^.ErrorID);
			internalEnableGroupState	:= 900;
		END_IF
		
	
	20: // ------------------------------------------------------- enable the group
		
		fbGroupEnable.Execute			:= TRUE;
		
		IF fbGroupEnable.Done THEN
			fbGroupEnable.Execute		:= FALSE;
			internalEnableGroupState	:= 100;
		ELSIF fbGroupEnable.Error THEN
			ErrorID						:= fbGroupEnable.ErrorId;
			ErrorOrigin					:= CONCAT( InstancePath, '.fbGroupEnable' );
			MsgCreate.CreateEx( TC_EVENTS.XtsBaseEventClass.fbGroupEnable,0 );
			MsgCreate.ipArguments.Clear().AddString(THIS^.InstancePath).AddUDINT(THIS^.ErrorID);
			MsgCreate.Send(0);
			internalEnableGroupState	:= 900;
		END_IF
		
	100: // clean up flags and rewind
		internalEnabled := TRUE;
		internalEnableGroupState := 0;
		
	900: // error
		Error := TRUE;
		internalEnabled := FALSE;
		
		
END_CASE

// catch error flag and reset custom status bit
IF (fbGroupStatus.GroupErrorStop) THEN
	Error := TRUE;
	internalEnabled := FALSE;
END_IF

// cyclic calls
fbGroupEnable( AxesGroup := GROUP_REF );
fbGroupReset( AxesGroup := GROUP_REF );
fbGroupStop( AxesGroup := GROUP_REF );

// always clear the triggering flag
internalEnableGroupFlag := FALSE;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="CyclicEnvironment" Id="{bec7bc22-417c-060d-2998-493fbd5e5180}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD CyclicEnvironment : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE nEnvironmentState OF
	0:	// ----------------------------------------- Enable Init Items
	
		// Enable init Info Server if needed
		stXtsEnvironmentConfiguration.bEnableInitInfoServer		:= TRUE;
		
		// Enable init CA Group if needed
		stXtsEnvironmentConfiguration.bEnableInitCaGroup		:= TRUE;
		
		// Set configuration of XtsUtility init
		fbXtsEnvironment.P_XtsEnvironmentConfiguration			:= stXtsEnvironmentConfiguration;
		
		// Next step
		nEnvironmentState	:= 1;
		
	1:	// ------------------------------------------ Init Environment
		
		// Start init
		IF fbXtsEnvironment.Init(TRUE) THEN
			// stop init
			fbXtsEnvironment.Init(FALSE);
			
			// next step
			nEnvironmentState	:= 2;
		END_IF
		
	2:	// ------------------------------------------ Check for success
	
		// Check IsInitialized property
		IF fbXtsEnvironment.P_IsInitialized THEN
			// assign to vars
			internalEnvironment := fbXtsEnvironment;
			internalXPU			:= fbXtsEnvironment.XpuTcIo(1);
			internalInfoServer	:= fbXtsEnvironment.InfoServerTcIo(1);
			// next step
			nEnvironmentState	:= 3;
		END_IF
		
	3:	// ------------------------------------------- Init Visu
	
		fbXtsEnvironmentVisu( ipTcIoXtsEnvironment := fbXtsEnvironment );
		
		// next step
		nEnvironmentState	:= 4;
		
	4:	// ------------------------------------------- Init done

		fbXtsEnvironmentVisu.Cycle();
		
END_CASE]]></ST>
      </Implementation>
    </Method>
    <Method Name="CyclicMover1" Id="{7d08ba4e-da2e-0f10-1c1f-401198736263}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD CyclicMover1 : BOOL
VAR
	eMoverIdDetectionMode : MoverIdDetectionMode;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// monitor for the group reset flag and act on it
CASE internalM1DetectState OF 
	0:	// wait for flag and envrionment
		IF internalM1DetectFlag AND EnvironmentIsReady AND_THEN NOT THIS^.Mover1DetectionComplete THEN
 			internalM1DetectState := 10;
		END_IF;
		
	10:	// check for mover 1 mode
		eMoverIdDetectionMode := XPU.GetMoverIdDetectionMode();
		IF (eMoverIdDetectionMode = MoverIdDetectionMode.Mover1) THEN
			// mover 1 detection sequence
			internalM1DetectState := 100;
		ELSE
			// no detection required
			internalM1DetectState := 200;
		END_IF
		
	100: // check if mover 1 id is avtive
		IF NOT (XPU.GetIsMoverIdDetectionActive() OR XPU.GetIsMoverIdDetectionValid()) THEN
			// start the detection
			XPU.TriggerMoverIdDetection();
		END_IF;
		internalM1DetectState := 110;
		
	110: // wait for group detection results
		IF XPU.GetHasMoverIdDetectionError() THEN
			// error state
			internalM1DetectState := 900;
		ELSIF XPU.GetIsMoverIdDetectionValid() AND NOT XPU.GetIsMoverIdDetectionActive() THEN
			// detection complete
			internalM1DetectState := 200;
		END_IF;
				
	
	200: // set flags and clean up
		// flag is read directly from XPU in Mover1DetectionComplete property
		internalM1DetectState := 0;
		
END_CASE

// reset flag always
internalM1DetectFlag := FALSE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="DetectMover1" Id="{1d387785-e892-0967-22d4-73d2923e67ae}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD DetectMover1 : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// calling enable group only sets a group reset flag that is handled in Cyclic Group Reset
// this effectively sums up requests from all movers into a single sequence that will handle the reset
internalM1DetectFlag := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="EnableGroup" Id="{a75c02a5-1ca0-0df7-3f91-6bd402a249c4}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD EnableGroup : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// calling enable group only sets a group reset flag that is handled in Cyclic Group Reset
// this effectively sums up requests from all movers into a single sequence that will handle the reset
internalEnableGroupFlag := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Property Name="EnvironmentIsReady" Id="{45272fae-93d1-0503-0b16-fd82b7738745}" FolderPath="Properties\">
      <Declaration><![CDATA[PROPERTY EnvironmentIsReady : BOOL]]></Declaration>
      <Get Name="Get" Id="{c1e17dfb-61a0-0ba8-0903-93fde5311796}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[EnvironmentIsReady := fbXtsEnvironment.P_IsInitialized;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Envrionment" Id="{5e5f4ba6-ff8f-0396-2883-6b9b059dc7fd}" FolderPath="Properties\">
      <Declaration><![CDATA[PROPERTY Envrionment : I_TcIoXtsEnvironment]]></Declaration>
      <Get Name="Get" Id="{67009401-6b19-0292-3a1b-93904af08df3}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[Envrionment := internalEnvironment;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="GroupEnabled" Id="{dca70979-fee3-0f39-3528-000ca64a8f22}" FolderPath="Properties\">
      <Declaration><![CDATA[PROPERTY GroupEnabled : bool]]></Declaration>
      <Get Name="Get" Id="{a4a17b6e-a2a8-0398-0745-96cf710d270e}">
        <Declaration><![CDATA[
VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[GroupEnabled := internalEnabled;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="GroupError" Id="{3ac97d16-5fe4-0a9a-3305-6d88385b660b}" FolderPath="Properties\">
      <Declaration><![CDATA[PROPERTY GroupError : bool]]></Declaration>
      <Get Name="Get" Id="{faa6b223-287a-0e59-1c37-f312dffb5ec6}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[// sum of various error bits throughout the mediator
GroupError := fbGroupStatus.GroupErrorStop;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="InfoServer" Id="{816a507a-b5f1-077f-3d31-6cc9a0af8cb5}" FolderPath="Properties\">
      <Declaration><![CDATA[PROPERTY InfoServer : I_TcIoXtsInfoServer]]></Declaration>
      <Get Name="Get" Id="{880f5fd5-5a90-005a-341d-63c8b7f4f011}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[InfoServer := internalInfoServer;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Mover1DetectionComplete" Id="{b3d53d33-3d35-07f8-35ed-d0dbe454a97c}" FolderPath="Properties\">
      <Declaration><![CDATA[PROPERTY Mover1DetectionComplete : BOOL]]></Declaration>
      <Get Name="Get" Id="{e92705b4-cdc3-0cc7-25dc-0275953572ce}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[Mover1DetectionComplete := XPU.GetIsMoverIdDetectionValid() AND NOT XPU.GetIsMoverIdDetectionActive();
]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="ResetStatistics" Id="{03455d1c-c490-0e78-3526-b69b05e0917b}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD ResetStatistics : BOOL
VAR
	i					: UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// reset statistics at all postion triggers and stations
FOR i := 0 TO GVL.NUM_POSITIONTRIGGERS-1 DO
	PositionTriggerArray[i]^.ResetStatistics();
END_FOR
FOR i := 0 TO GVL.NUM_STATIONS-1 DO
	StationArray[i]^.ResetStatistics();
END_FOR
]]></ST>
      </Implementation>
    </Method>
    <Property Name="XPU" Id="{b9694a68-ee0f-0a17-2999-0aade43c6ce4}" FolderPath="Properties\">
      <Declaration><![CDATA[PROPERTY XPU : I_TcIoXtsProcessingUnit]]></Declaration>
      <Get Name="Get" Id="{fb7fa487-baed-05fa-2f8e-e167e0e16d6f}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[XPU := internalXPU;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <LineIds Name="Mediator">
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="Mediator.AddMover">
      <LineId Id="9" Count="1" />
      <LineId Id="5" Count="0" />
      <LineId Id="12" Count="0" />
      <LineId Id="11" Count="0" />
      <LineId Id="13" Count="0" />
      <LineId Id="40" Count="0" />
      <LineId Id="39" Count="0" />
      <LineId Id="15" Count="1" />
      <LineId Id="20" Count="1" />
      <LineId Id="26" Count="3" />
      <LineId Id="31" Count="1" />
      <LineId Id="22" Count="0" />
      <LineId Id="35" Count="1" />
      <LineId Id="71" Count="0" />
      <LineId Id="37" Count="0" />
      <LineId Id="72" Count="0" />
      <LineId Id="57" Count="0" />
      <LineId Id="53" Count="0" />
      <LineId Id="60" Count="0" />
      <LineId Id="64" Count="0" />
      <LineId Id="63" Count="0" />
      <LineId Id="61" Count="0" />
      <LineId Id="65" Count="0" />
      <LineId Id="73" Count="0" />
      <LineId Id="66" Count="1" />
      <LineId Id="69" Count="1" />
      <LineId Id="68" Count="0" />
      <LineId Id="52" Count="0" />
      <LineId Id="38" Count="0" />
      <LineId Id="14" Count="0" />
    </LineIds>
    <LineIds Name="Mediator.AddMoverList">
      <LineId Id="15" Count="1" />
      <LineId Id="5" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="35" Count="1" />
      <LineId Id="18" Count="0" />
      <LineId Id="20" Count="3" />
      <LineId Id="25" Count="0" />
      <LineId Id="27" Count="3" />
      <LineId Id="26" Count="0" />
      <LineId Id="24" Count="0" />
      <LineId Id="31" Count="3" />
      <LineId Id="19" Count="0" />
    </LineIds>
    <LineIds Name="Mediator.AddPositionTrigger">
      <LineId Id="18" Count="3" />
      <LineId Id="38" Count="1" />
      <LineId Id="22" Count="14" />
      <LineId Id="52" Count="0" />
      <LineId Id="61" Count="0" />
      <LineId Id="53" Count="0" />
      <LineId Id="57" Count="0" />
      <LineId Id="59" Count="1" />
      <LineId Id="58" Count="0" />
      <LineId Id="56" Count="0" />
      <LineId Id="37" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="Mediator.AddStation">
      <LineId Id="36" Count="0" />
      <LineId Id="17" Count="2" />
      <LineId Id="37" Count="1" />
      <LineId Id="20" Count="15" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="Mediator.AddTrack">
      <LineId Id="17" Count="3" />
      <LineId Id="37" Count="1" />
      <LineId Id="21" Count="15" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="Mediator.AddZone">
      <LineId Id="17" Count="3" />
      <LineId Id="37" Count="1" />
      <LineId Id="21" Count="14" />
      <LineId Id="51" Count="3" />
      <LineId Id="56" Count="1" />
      <LineId Id="55" Count="0" />
      <LineId Id="58" Count="0" />
      <LineId Id="36" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="Mediator.CompleteMoverList.Get">
      <LineId Id="2" Count="0" />
      <LineId Id="6" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="7" Count="0" />
      <LineId Id="13" Count="0" />
      <LineId Id="12" Count="0" />
      <LineId Id="14" Count="0" />
      <LineId Id="16" Count="0" />
      <LineId Id="15" Count="0" />
    </LineIds>
    <LineIds Name="Mediator.Cyclic">
      <LineId Id="117" Count="29" />
      <LineId Id="116" Count="0" />
    </LineIds>
    <LineIds Name="Mediator.CyclicEnableGroup">
      <LineId Id="6" Count="2" />
      <LineId Id="82" Count="0" />
      <LineId Id="87" Count="0" />
      <LineId Id="89" Count="2" />
      <LineId Id="135" Count="0" />
      <LineId Id="86" Count="0" />
      <LineId Id="85" Count="0" />
      <LineId Id="32" Count="1" />
      <LineId Id="39" Count="12" />
      <LineId Id="125" Count="1" />
      <LineId Id="52" Count="3" />
      <LineId Id="123" Count="1" />
      <LineId Id="56" Count="4" />
      <LineId Id="66" Count="8" />
      <LineId Id="121" Count="1" />
      <LineId Id="120" Count="0" />
      <LineId Id="75" Count="0" />
      <LineId Id="27" Count="0" />
      <LineId Id="92" Count="1" />
      <LineId Id="95" Count="1" />
      <LineId Id="101" Count="3" />
      <LineId Id="94" Count="0" />
      <LineId Id="76" Count="1" />
      <LineId Id="106" Count="0" />
      <LineId Id="78" Count="1" />
      <LineId Id="105" Count="0" />
      <LineId Id="80" Count="1" />
      <LineId Id="111" Count="3" />
      <LineId Id="110" Count="0" />
      <LineId Id="98" Count="0" />
      <LineId Id="97" Count="0" />
      <LineId Id="99" Count="1" />
    </LineIds>
    <LineIds Name="Mediator.CyclicEnvironment">
      <LineId Id="6" Count="29" />
      <LineId Id="51" Count="3" />
      <LineId Id="36" Count="14" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="Mediator.CyclicMover1">
      <LineId Id="5" Count="0" />
      <LineId Id="28" Count="2" />
      <LineId Id="34" Count="4" />
      <LineId Id="40" Count="1" />
      <LineId Id="43" Count="3" />
      <LineId Id="42" Count="0" />
      <LineId Id="47" Count="1" />
      <LineId Id="50" Count="1" />
      <LineId Id="55" Count="0" />
      <LineId Id="54" Count="0" />
      <LineId Id="49" Count="0" />
      <LineId Id="57" Count="3" />
      <LineId Id="62" Count="0" />
      <LineId Id="61" Count="0" />
      <LineId Id="65" Count="0" />
      <LineId Id="63" Count="1" />
      <LineId Id="56" Count="0" />
      <LineId Id="70" Count="3" />
      <LineId Id="39" Count="0" />
      <LineId Id="6" Count="0" />
      <LineId Id="67" Count="0" />
      <LineId Id="66" Count="0" />
      <LineId Id="68" Count="0" />
    </LineIds>
    <LineIds Name="Mediator.DetectMover1">
      <LineId Id="5" Count="1" />
      <LineId Id="8" Count="0" />
    </LineIds>
    <LineIds Name="Mediator.EnableGroup">
      <LineId Id="5" Count="1" />
      <LineId Id="8" Count="0" />
    </LineIds>
    <LineIds Name="Mediator.EnvironmentIsReady.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="Mediator.Envrionment.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="Mediator.GroupEnabled.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="Mediator.GroupError.Get">
      <LineId Id="5" Count="0" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="Mediator.InfoServer.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="Mediator.Mover1DetectionComplete.Get">
      <LineId Id="2" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="Mediator.ResetStatistics">
      <LineId Id="5" Count="0" />
      <LineId Id="13" Count="5" />
      <LineId Id="12" Count="0" />
    </LineIds>
    <LineIds Name="Mediator.XPU.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>